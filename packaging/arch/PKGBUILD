pkgbase=timetracker
pkgname=timetracker
pkgver=8f2d343
pkgrel=1
arch=("x86_64")
url="https://github.com/NorthernLightsDevel/TimeTracker"
license=("MIT")
_source_url="${TIMETRACKER_SOURCE_URL:-https://github.com/NorthernLightsDevel/TimeTracker.git#branch=main}"
source=("$pkgbase::git+$_source_url")
options=('!strip' '!debug')
sha256sums=('SKIP')

pkgver() {
    cd "$srcdir/$pkgbase"
    git describe --tags --long --always 2>/dev/null | sed 's/^v//; s/-/./g'
}

prepare() {
    export DOTNET_CLI_TELEMETRY_OPTOUT=1
    export DOTNET_NOLOGO=1

    cd "$srcdir/$pkgbase"
    dotnet restore TimeTracker.sln
}

build() {
    export DOTNET_CLI_TELEMETRY_OPTOUT=1
    export DOTNET_NOLOGO=1

    cd "$srcdir/$pkgbase"

    local publish_args=(
        -c Release
        -r linux-x64
        --self-contained true
        -p:PublishSingleFile=true
    )

    dotnet publish src/TimeTracker.Cli/TimeTracker.Cli.csproj "${publish_args[@]}" -o "$srcdir/publish-cli"
    dotnet publish src/TimeTracker.Desktop/TimeTracker.Desktop.csproj "${publish_args[@]}" -o "$srcdir/publish-desktop"
    dotnet publish src/TimeTracker.Api/TimeTracker.Api.csproj "${publish_args[@]}" -o "$srcdir/publish-api"
}

package() {
  install -d "$pkgdir/usr/lib/$pkgname"
  install -d "$pkgdir/usr/bin/"
  install -d "$pkgdir/usr/share/applications/"
  install -d "$pkgdir/usr/lib/systemd/system"

  install -d "$pkgdir/usr/lib/$pkgname/desktop"
  cp -r publish-desktop/* "$pkgdir/usr/lib/$pkgname/desktop/"
  ln -sf "/usr/lib/$pkgname/desktop/TimeTracker.Desktop" "$pkgdir/usr/bin/timetracker-desktop"

  install -d "$pkgdir/usr/lib/$pkgname/cli"
  cp -r publish-cli/* "$pkgdir/usr/lib/$pkgname/cli/"
  ln -sf "/usr/lib/$pkgname/cli/TimeTracker.Cli" "$pkgdir/usr/bin/timetracker"

  install -d "$pkgdir/usr/lib/$pkgname/api"
  cp -r publish-api/* "$pkgdir/usr/lib/$pkgname/api/"
  ln -sf "/usr/lib/$pkgname/api/TimeTracker.Api" "$pkgdir/usr/bin/timetracker-api"

  install -Dm644 "$srcdir/$pkgbase/packaging/systemd/timetracker-api.service" "$pkgdir/usr/lib/systemd/system/timetracker-api.service"
  install -Dm644 /dev/stdin "$pkgdir/usr/lib/systemd/system-preset/90-timetracker.preset" <<'EOF'
enable timetracker-api.service
EOF

  install -Dm755 "$srcdir/$pkgbase/scripts/waybar-timetracker.sh" "$pkgdir/usr/bin/timetracker-waybar"
  install -Dm644 /dev/stdin "$pkgdir/usr/share/applications/timetracker.desktop" <<'EOF'
[Desktop Entry]
Version=1.0
Type=Application
Name=TimeTracker
Comment=Track work hours and tasks
Exec=/usr/bin/timetracker-desktop
Terminal=false
Categories=Office;Utility;TimeTracking;
StartupNotify=true
EOF
}

